%This is the warehouse from TTYD64.
%Replace the Shine Sprite with a Star Piece and add a little pedestal.


#new:Header $Header
{
	[MainScript] $Script_Main
	[Background] 80200000
	[EntryList] $EntryList
	[EntryCount] 1
    [MapTattle]  $Function_GetTattle
}

#new:EntryList $EntryList
{
	~Vec4f:Entry0
}

#new:Script_Main $Script_Main
{
	Set   *GB_WorldLocation  .Location:KoopaBrosFortress
	Call  SetCamPerspective  ( .Cam:Default 00000003 00000019 00000010 00001000 )
	Call  SetCamBGColor      ( .Cam:Default 00000000 00000000 00000000 )
	Call  SetCamEnabled      ( .Cam:Default .True  )
	Call  SetCamLeadPlayer   ( .Cam:Default .False )
    Exec  $Script_MakeEntities
	Exec  $Script_SetupMusic
	Exec  $Script_EnterMap
	Return
	End
}

#new:Script $Script_SetupMusic
{
	Call  SetMusicTrack ( 00000000 .Song:KoopaFortress 00000000 00000008 )
	Call  ClearAmbientSounds ( 250` )
	Return
	End
}

#new:Script $Script_EnterMap
{
	Call  GetEntryID    ( *Var[0] )
	Switch  *Var[0]
		Case  ==  ~Entry:Entry0
			Set   *Var[0] $Script_CreateExitTriggers
			Exec  EnterWalk
		Default
			Exec  $Script_CreateExitTriggers
	EndSwitch
	Return
	End
}

#new:Script $Script_Exit0
{
	SetGroup 0000001B
	Call     UseExitHeading ( 60` ~Entry:Entry0 )
	Exec     ExitWalk
	Call  GotoMap   ( "trd_00" 00000007 )
	Wait  50`
	Return
	End
}

#new:Script $Script_CreateExitTriggers
{
	Bind  $Script_Exit0 .Trigger:FloorAbove ~Collider:Exit0 00000001 00000000
	Return
	End
}

#new:Script $Script_MakeEntities
{
    Call  MakeItemEntity    ( .Item:StarPiece ~Vec3d:StarPiece .ItemSpawnMode:Fixed_NeverVanish *GF_TRD11_StarPiece )
    Return
    End
}


% Tattle

#new:Function $Function_GetTattle
{
LIO       V0, $MapTattle
JR        RA
RESERVED
}

#string $MapTattle
{
[Style Tattle]
[EnableCDownNext]Uh, this looks like some kind of[BR]
warehouse.
[Wait][End]
}